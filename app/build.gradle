import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

task ndkBuild(type: Exec, description: 'run ndk-build') {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir 'src/main/jni'
        commandLine 'ndk-build.cmd', '-j', Runtime.runtime.availableProcessors()

    } else {
        workingDir 'src/main/jni'
        commandLine "$ndkDir/ndk-build", '-j', Runtime.runtime.availableProcessors()
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(ndkBuild)
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 52
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            //runProguard true
            proguardFile file('android.pro')
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a'
            universalApk true
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 6, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 100 + android.defaultConfig.versionCode
        }
    }
    sourceSets {
        main {
            //jniLibs.srcDirs = ['native-libs']
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }

    productFlavors {
        playstore {
        }
        develop {
        }
    }

    lintOptions {
        //I'm too lazy to translate everything
        abortOnError false
    }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    //compile project(':tess-two:tess-two')
    compile fileTree(dir: 'tess-two', include: ['tess-two'])
    compile fileTree(dir: 'src/main/libs', include: '*.jar')
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'

    //compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.mcxiaoke.viewpagerindicator:library:2.4.1@aar'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.code.findbugs:jsr305:2.0.2'
    compile 'com.google.guava:guava:18.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.apache.commons:commons-compress:1.8.1'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'
    compile 'com.github.worker8:RadioGroupPlus:v1.0.1'

    //mip
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'org.mockito:mockito-core:1.10.17'
    androidTestCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2+') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.2@aar') {
        transitive = true;
    }
}

//MIP
//Remove comment to exclude cwac-cam2 dependency to make the capture feature unavailable
//configurations.compile.exclude module: 'cam2'

crashlytics {//todo
    enableNdk true
    androidNdkOut '/Users/renard/devel/textfairy/app/src/main/obj'
    androidNdkLibsOut '/Users/renard/devel/textfairy/app/src/main/libs'
}